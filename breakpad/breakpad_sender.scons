# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()

env.Prepend(
    CPPPATH = [
        'src',
        '$CHROME_SRC_DIR',
    ],
)

if env.Bit('windows'):
  env.Append(
      CCFLAGS = [
          '/TP',
          '/wd4800',
      ],
  )

input_files = ChromeFileList([
    'src/client/windows/sender/crash_report_sender.cc',
    'src/common/windows/http_upload.cc',
    'src/client/windows/sender/crash_report_sender.h',
    'src/common/windows/http_upload.h',
])

if env.Bit('windows'):
  env.ChromeLibrary('breakpad_sender', input_files)

p = env.ChromeMSVSProject('breakpad_sender.vcproj',
                          dest=('$CHROME_SRC_DIR/breakpad/' +
                                    'breakpad_sender.vcproj'),
                          guid='{9946A048-043B-4F8F-9E07-9297B204714C}',
                          keyword='Win32Proj',
                          # TODO(sgk):  when we can intuit the hierarchy
                          # from the built targets.
                          #buildtargets=TODO,
                          files=input_files,
                          tools=[
                              'VCLibrarianTool',
                              'VCCLCompilerTool',
                          ],
                          ConfigurationType='4')

p.AddConfig('Debug|Win32',
            InheritedPropertySheets=[
                '$(SolutionDir)../build/common.vsprops',
                '$(SolutionDir)../build/debug.vsprops',
                '$(SolutionDir)../build/external_code.vsprops',
                'using_breakpad.vsprops',
            ])

p.AddConfig('Release|Win32',
            InheritedPropertySheets=[
                '$(SolutionDir)../build/common.vsprops',
                '$(SolutionDir)../build/release.vsprops',
                '$(SolutionDir)../build/external_code.vsprops',
                'using_breakpad.vsprops',
            ])
