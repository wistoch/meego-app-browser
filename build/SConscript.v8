# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()

mksnapshot_exe = File('#/../v8/bin/debug/mksnapshot.exe')
v8_exe = File('#/../v8/bin/release/v8.exe')
v8_lib = File('#/../v8/bin/debug/v8.lib'),

v8_scons_targets = [
    mksnapshot_exe,
    v8_exe,
    v8_lib,
    File('#/../v8/bin/debug/snapshot-empty.obj'),
    File('#/../v8/vc80.pdb')
]

v8 = env.Command(v8_scons_targets,
                 [],
                 'cd ..\\v8 && $PYTHON $SCONS $SCONSFLAGS ${TARGETS[:-1]}',
                 SCONS='..\\third_party\\scons\\scons.py',
                 SCONSFLAGS='-Q --warn=no-deprecated')
env.AlwaysBuild(v8)
env.Install('$V8_DIR', v8)

i = env.Install('$TARGET_ROOT', mksnapshot_exe)
env.Alias('webkit', i)

i = env.Install('$LIBS_DIR', v8_lib)
env.Alias('webkit', i)

i = env.Install('$TARGET_ROOT', v8_exe)
env.Alias('chrome', i)

# To satisfy tests expecting the following .exe name.
i = env.InstallAs('$TARGET_ROOT/v8_shell_sample.exe', v8_exe)
env.Alias('chrome', i)

# Tell our SCons invocation to *not* delete v8.lib and the other targets
# before building them, so the subsidiary v8 SCons call doesn't always
# rebuild them (thereby causing us to always rebuild their dependents).
env.Precious(v8)
