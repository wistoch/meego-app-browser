# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env_test')

env_test = env_test.Clone()

env_test.Prepend(
    CPPPATH = [
        '$CHROME_DIR/app/resources',
        '$ICU38_DIR/public/common',
        '$ICU38_DIR/public/i18n',
        '$SKIA_DIR/include',
        '$SKIA_DIR/include/corecg',
        '$SKIA_DIR/platform',
        '$CHROME_SRC_DIR',
        '$GTEST_DIR/include',
        'third_party/wtl/include',
        '$LIBXSLT_DIR',
        '$LIBXML_DIR/include',
        '$LIBXML_DIR/DerivedSources/include',
        'tools/build/win',
    ],
    CPPDEFINES = [
        'LIBXSLT_STATIC',
        'LIBXML_STATIC',
        'PNG_USER_CONFIG',
        'CHROME_PNG_WRITE_SUPPORT',
        'UI_TEST',
        'UNIT_TEST',
    ],
    LIBS = [
        'libxml',
        'googleurl',
        'skia',
        'libpng',
        'npapi_layout_test_plugin',
        'gtest',
        'base_gfx',
        env_test['ICU_LIBS'],  # TODO(sgk):  '$ICU_LIBS' when scons is fixed
        'zlib',
        'modp_b64',
        'net',
        'bzip2',
        'base',
        'npapi_test_plugin',
    ],
)

if env_test['PLATFORM'] == 'win32':
  env_test.Prepend(
      LINKFLAGS = [
          '/INCREMENTAL',

          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',

          '/MANIFEST',
          '/DELAYLOAD:"ws2_32.dll"',
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',

          '/DEBUG',
          '/MACHINE:X86',
          '/FIXED:No',
      ],
      LIBS = [
          # TODO(sgk):  to be ported to Mac and Linux
          'sdch',

          'comsupp.lib',
          'oleacc.lib',
          'rpcrt4.lib',
          'winmm.lib',
      ],
  )

libs = [
    'test/security_tests/security_tests.lib',
    'common/common.lib',
    'browser/browser.lib',
    'browser/views/browser_views.lib',
    'test/automation/automation.lib',
]

ui_test_files = [
    'app/chrome_main_uitest.cc',
    'browser/browser_uitest.cc',
    'browser/crash_recovery_uitest.cc',
    'browser/download/download_uitest.cc',
    'browser/download/save_page_uitest.cc',
    'browser/errorpage_uitest.cc',
    'browser/history/redirect_uitest.cc',
    'browser/iframe_uitest.cc',
    'browser/images_uitest.cc',
    'browser/interstitial_page_uitest.cc',
    'browser/locale_tests_uitest.cc',
    'browser/login_prompt_uitest.cc',
    'browser/metrics_service_uitest.cc',
    'browser/printing/printing_layout_uitest.cc',
    'browser/resource_dispatcher_host_uitest.cc',
    'browser/sanity_uitest.cc',
    'browser/session_history_uitest.cc',
    'browser/session_restore_uitest.cc',
    'browser/ssl_uitest.cc',
    'browser/tab_restore_uitest.cc',
    'browser/view_source_uitest.cc',
    'common/logging_chrome_uitest.cc',
    'common/net/cache_uitest.cc',
    'common/pref_service_uitest.cc',
    'test/accessibility/accessibility_tests.cc',
    'test/accessibility/accessibility_util.cc',
    'test/accessibility/browser_impl.cc',
    'test/accessibility/keyboard_util.cc',
    'test/accessibility/registry_util.cc',
    'test/accessibility/tab_impl.cc',
    'test/automation/automation_proxy_uitest.cc',
    'test/perf/mem_usage.cc',
    'test/reliability/page_load_test$OBJSUFFIX',
    'test/ui/inspector_controller_uitest.cc',
    'test/ui/layout_plugin_uitest.cpp',
    'test/ui/npapi_uitest.cpp',
    'test/ui/omnibox_uitest.cc',
    'test/ui/run_all_unittests.cc',
    'test/ui/sandbox_uitests.cc',
    'test/ui/ui_test.cc',
    'test/ui/ui_test_suite.cc',
    'test/ui/history_uitest.cc',
    'test/ui/npapi_test_helper.cc',
    '$CHROME_DIR/test/test_file_util$OBJSUFFIX',
    '$NET_DIR/url_request/url_request_test_job$OBJSUFFIX',
]

if env_test['PLATFORM'] == 'win32':
  ui_test_files.extend([
      'find_bar_win_interactive_uitest.cc',
      'find_bar_win_uitest.cc',
  ])

ui_tests = env_test.ChromeTestProgram('ui_tests',  ui_test_files + libs)

i = env_test.Install('$TARGET_ROOT', ui_tests)
Alias('chrome', i)

