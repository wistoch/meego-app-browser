# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env', 'env_res')

env = env.Clone()
env_res = env_res.Clone()


env_res.Append(
  CPPPATH = [
    "$TARGET_ROOT",
    ".",
    "#/..",
  ],
  RCFLAGS = [
    ["/l", "0x409"],
  ],
)


resources = [
    env_res.RES('setup.rc'),
]


env.Prepend(
    CPPPATH = [
        '../util',
        '$TARGET_ROOT',
        '.',
        '#/..',
    ],
    LIBS = [
        'base',
        'bspatch',
        'common',
        env['ICU_LIBS'],  # TODO(sgk):  '$ICU_LIBS' when scons is fixed
        'lzma_sdk',
        'util',
    ],
)

env.Prepend(
    LINKFLAGS = [
        '/INCREMENTAL',
        '/DEBUG',

        '/DELAYLOAD:"dwmapi.dll"',
        '/DELAYLOAD:"uxtheme.dll"',

        '/OPT:NOWIN98',
        '/SUBSYSTEM:WINDOWS',
        '/MACHINE:X86',
        '/FIXED:No',

        '/safeseh',
        '/dynamicbase',
        '/ignore:4199',
        '/nxcompat',

        '/PDB:${TARGETS[1]}',
        '/MAP:${TARGETS[2]}',
    ],
    LIBS = [
        'msi.lib',
    ],
)

input_files = [
    'install.cc',
    'main.cc',
    'setup.cc',
    'setup_constants.cc',
    'uninstall.cc',
]

if env['PLATFORM'] == 'win32':
  targets = [
      'setup.exe',
      'setup.pdb',
      'setup.lib',
  ]
else:
  targets = ['setup']

exe = env.ChromeProgram(targets, resources + input_files)
i = env.Install('$TARGET_ROOT', exe)

env.Alias('chrome', i)


env.ChromeVersionRC('setup_exe_version.rc',
                    'setup_exe_version.rc.version',
                    PWD = env.Dir('.'))

