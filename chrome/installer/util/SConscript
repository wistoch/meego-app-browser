# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()


env.Prepend(
    CPPPATH = [
        '$LZMA_SDK_DIR',
        '$CHROME_DIR/third_party/wtl/include',
        '$NPAPI_DIR',
        '$LIBXML_DIR/include',
        '$SKIA_DIR/include',
        '$SKIA_DIR/include/corecg',
        '$SKIA_DIR/platform',
        '$LIBPNG_DIR',
        '$ZLIB_DIR',
        '$BREAKPAD_DIR/src',
        '$LIBJPEG_DIR',
        '$ICU38_DIR/public/common',
        '$ICU38_DIR/public/i18n',
        '$CHROME_SRC_DIR',
        '.',
    ],
    CPPDEFINES = [
        "_LZMA_IN_CB",
        "LIBXML_STATIC",
        "PNG_USER_CONFIG",
        "CHROME_PNG_WRITE_SUPPORT"
        "U_STATIC_IMPLEMENTATION",
    ],
    CCFLAGS = [
        '/TP',
    ],
)

input_files = [
    'browser_distribution.cc',
    'copy_tree_work_item.cc',
    'create_dir_work_item.cc',
    'create_reg_key_work_item.cc',
    'delete_tree_work_item.cc',
    'google_chrome_distribution.cc',
    'google_update_constants.cc',
    'google_update_settings.cc',
    'helper.cc',
    'install_util.cc',
    'l10n_string_util.cc',
    'logging_installer.cc',
    'lzma_util.cc',
    'set_reg_value_work_item.cc',
    'shell_util.cc',
    'util_constants.cc',
    'version.cc',
    'work_item.cc',
    'work_item_list.cc',
]

x = env.ChromeStaticLibrary('util', input_files)


# create_string_rc.py imports FP.py from the tools/grit/grit/extern
# directory, so add that to PYTHONPATH for this command execution.
env_x = env.Clone()
env_x.AppendENVPath('PYTHONPATH',
    [env_x.Dir('$CHROME_SRC_DIR/tools/grit/grit/extern').abspath])
env_x.Command(['$CHROME_DIR/installer/util/installer_util_strings.rc',
               '$CHROME_DIR/installer/util/installer_util_strings.h'],
              ['$CHROME_DIR/installer/util/prebuild/create_string_rc.py',
               '$CHROME_DIR/app/generated_resources.grd'] +
               env.Glob('$CHROME_DIR/app/resources/*.xtb'),
              "$PYTHON ${SOURCES[0]} ${TARGET.dir}")
