# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env_test')

env_test = env_test.Clone()


env_test.Prepend(
    CPPDEFINES = [
        'UI_TEST',
        'UNIT_TEST',
        'LIBXSLT_STATIC',
        'LIBXML_STATIC',
    ],
    CPPPATH = [
        '$GTEST_DIR/include',
        '$GTEST_DIR',
        '$SKIA_DIR/include',
        '$SKIA_DIR/include/corecg',
        '$SKIA_DIR/platform',
        '$CHROME_SRC_DIR',
        '$CHROME_DIR/third_party/wtl/include',
        '$LIBXSLT_DIR',
        '$LIBXML_DIR/include',
    ],
    LIBS = [
        'automation',
        'base',
        'base_gfx',
        'browser',
        'browser_views',
        'bzip2',
        'common',
        'googleurl',
        'gtest',
        env_test['ICU_LIBS'],  # TODO(sgk):  '$ICU_LIBS' when scons is fixed
        'libpng',
        'modp_b64',
        'net',
        'skia',
        'zlib',
    ],
)

env_test.Prepend(
    LINKFLAGS = [
        '/INCREMENTAL',
        '/DEBUG',

        '/DELAYLOAD:"dwmapi.dll"',
        '/DELAYLOAD:"uxtheme.dll"',

        '/MACHINE:X86',
        '/FIXED:No',

        '/safeseh',
        '/dynamicbase',
        '/ignore:4199',
        '/nxcompat',
    ],
    LIBS = [
        'comsupp.lib',
        'oleacc.lib',
        'psapi.lib',
        'rpcrt4.lib',
        'winmm.lib',
    ],
)

input_files = [
    '$CHROME_DIR/browser/browser_focus_uitest.cc',
    '$CHROME_DIR/browser/views/bookmark_bar_view_test.cc',
    '$CHROME_DIR/browser/views/constrained_window_impl_interactive_uitest.cc',
    '$CHROME_DIR/browser/views/tabs/tab_dragging_test.cc',
    '$CHROME_DIR/test/ui/run_all_unittests$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test_suite$OBJSUFFIX',
    '$CHROME_DIR/test/test_file_util$OBJSUFFIX',
    'view_event_test_base.cc',
    '$CHROME_DIR/test/ui/npapi_test_helper$OBJSUFFIX',
    '$CHROME_DIR/test/interactive_ui/npapi_interactive_test.cc',
]

exe = env_test.ChromeTestProgram('interactive_ui_tests', input_files)
i = env_test.Install('$TARGET_ROOT', exe)

env_test.Alias('chrome', i)
