# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env_test')

env_test = env_test.Clone()


env_test.Prepend(
    CPPDEFINES = [
        'UI_TEST',
        'UNIT_TEST',
    ],
    CPPPATH = [
        '$GTEST_DIR/include',
        '$GTEST_DIR',
        '$SKIA_DIR/include',
        '$SKIA_DIR/include/corecg',
        '$SKIA_DIR/platform',
        '#/..',
    ],
    LIBS = [
        'automation',
        'base',
        'base_gfx',
        'browser',
        'browser_views',
        'common',
        'googleurl',
        'gtest',
        env_test['ICU_LIBS'],  # TODO(sgk):  '$ICU_LIBS' when scons is fixed
        'libpng',
        'net',
        'skia',
        'zlib',
    ],
)

if env_test['PLATFORM'] == 'win32':
  env_test.Prepend(
      LINKFLAGS = [
          '/INCREMENTAL',
          '/DEBUG',

          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',

          '/MACHINE:X86',
          '/FIXED:No',

          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
  )

input_files = [
    '$CHROME_DIR/test/perf/mem_usage$OBJSUFFIX',
    '$CHROME_DIR/test/ui/run_all_unittests$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test_suite$OBJSUFFIX',
    '$CHROME_DIR/test/test_file_util$OBJSUFFIX',
    'memory_test.cc',
]

exe = env_test.ChromeTestProgram('memory_test', input_files)
i = env_test.Install('$TARGET_ROOT', exe)

env_test.Alias('chrome', i)

