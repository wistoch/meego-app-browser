# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()

env.ApplySConscript([
    '$BASE_DIR/using_base.scons',
    '$BASE_DIR/gfx/using_base_gfx.scons',
    '$CHROME_SRC_DIR/build/using_googleurl.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$LIBPNG_DIR/using_libpng.scons',
    '$MODP_B64_DIR/using_modp_b64.scons',
    '$NET_DIR/using_net.scons',
    '$SKIA_DIR/using_skia.scons',
    '$ZLIB_DIR/using_zlib.scons',
])

env.Prepend(
    CPPDEFINES = [
        'UI_TEST',
    ],
    CPPPATH = [
        '$CHROME_SRC_DIR',
    ],
    LIBS = [
        'automation',
        'browser',
        'browser_views',
        'common',
    ],
)

if env.Bit('windows'):
  env.Prepend(
      LINKFLAGS = [
          '/INCREMENTAL',
          '/DEBUG',

          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',

          '/MACHINE:X86',
          '/FIXED:No',

          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
      LIBS = [
          'comsupp',
          'oleacc',
          'rpcrt4',
          'winmm',
      ],
  )

input_files = [
    '$CHROME_DIR/test/ui/run_all_unittests$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test$OBJSUFFIX',
    '$CHROME_DIR/test/ui/ui_test_suite$OBJSUFFIX',
    'feature_startup_test.cc',
    'startup_test.cc',
]

# TODO(port):
if env.Bit('windows'):
  env.ChromeTestProgram('startup_tests', input_files)

env.ChromeMSVSProject('$CHROME_DIR/test/startup/startup_tests.vcproj',
                      dependencies = [
                          '$CHROME_DIR/test/automation/automation.vcproj',
                          '$BASE_DIR/build/base.vcproj',
                          '$NET_DIR/build/net.vcproj',
                          '$CHROME_DIR/browser/browser.vcproj',
                          '$MODP_B64_DIR/modp_b64.vcproj',
                          '$CHROME_DIR/app/chrome_exe.vcproj',
                          '$ZLIB_DIR/zlib.vcproj',
                          '$CHROME_DIR/common/common.vcproj',
                          '$ICU38_DIR/build/icu.vcproj',
                          '$BASE_DIR/build/base_gfx.vcproj',
                          '$TESTING_DIR/gtest.vcproj',
                          '$LIBPNG_DIR/libpng.vcproj',
                          '$SKIA_DIR/skia.vcproj',
                          '$GOOGLEURL_DIR/build/googleurl.vcproj',
                          '$CHROME_DIR/browser/views/browser_views.vcproj',
                      ],
                      guid='{D3E6C0FD-54C7-4FF2-9AE1-72F2DAFD820C}')
