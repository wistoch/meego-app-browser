# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()

env.SConscript([
    '$BASE_DIR/using_base.scons',
    '$BASE_DIR/gfx/using_base_gfx.scons',
    '$BZIP2_DIR/using_bzip2.scons',
    '$CHROME_DIR/third_party/wtl/using_wtl.scons',
    '$CHROME_SRC_DIR/build/using_googleurl.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$LIBPNG_DIR/using_libpng.scons',
    '$LIBXML_DIR/using_libxml.scons',
    '$LIBXSLT_DIR/using_libxslt.scons',
    '$MODP_B64_DIR/using_modp_b64.scons',
    '$NET_DIR/using_net.scons',
    #'$NPAPI_DIR/using_npapi.scons',
    '$SDCH_DIR/using_sdch.scons',
    '$SKIA_DIR/using_skia.scons',
    '$ZLIB_DIR/using_zlib.scons',
], {'env':env})

env.Prepend(
    CPPPATH = [
        '$CHROME_DIR/app/resources',
        '$CHROME_SRC_DIR',
    ],
    CPPDEFINES = [
        'UI_TEST',
    ],
    LIBS = [
        'browser',
        'common',
        'sqlite',
    ]
)

if env.Bit('windows'):
  # TODO(port): split these into upper env.Prepend (for portable libs) and
  # lower env.Prepend (for win-specific libs).
  env.Prepend(
      LIBS = [
          'automation',
          'browser_views',
          'npapi_layout_test_plugin',
          'npapi_test_plugin',
          'port',
          'security_tests',
      ],
  )

if env.Bit('windows'):
  env.Prepend(
      CPPPATH = [
          '$CHROME_DIR/tools/build/win',
      ],
      LINKFLAGS = [
          '/INCREMENTAL',

          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',

          '/MANIFEST',
          '/DELAYLOAD:"ws2_32.dll"',
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',

          '/DEBUG',
          '/MACHINE:X86',
          '/FIXED:No',
      ],
      LIBS = [
          'comsupp',
          'oleacc',
          'rpcrt4',
          'winmm',
      ],
  )

ui_test_files = [
    'run_all_unittests.cc',
    'ui_test.cc',
    'ui_test_suite.cc',
]

if env.Bit('windows'):
  # TODO(port): mark which of these work and which don't.
  ui_test_files.extend([
    'history_uitest.cc',
    'inspector_controller_uitest.cc',
    'layout_plugin_uitest.cpp',
    'npapi_test_helper.cc',
    'npapi_uitest.cpp',
    'omnibox_uitest.cc',

    'sandbox_uitests.cc',

    '$CHROME_DIR/app/chrome_main_uitest.cc',
    '$CHROME_DIR/browser/browser_uitest.cc',
    '$CHROME_DIR/browser/crash_recovery_uitest.cc',
    '$CHROME_DIR/browser/download/download_uitest.cc',
    '$CHROME_DIR/browser/download/save_page_uitest.cc',
    '$CHROME_DIR/browser/errorpage_uitest.cc',
    '$CHROME_DIR/browser/history/redirect_uitest.cc',
    '$CHROME_DIR/browser/iframe_uitest.cc',
    '$CHROME_DIR/browser/images_uitest.cc',
    '$CHROME_DIR/browser/interstitial_page_uitest.cc',
    '$CHROME_DIR/browser/locale_tests_uitest.cc',
    '$CHROME_DIR/browser/login_prompt_uitest.cc',
    '$CHROME_DIR/browser/metrics_service_uitest.cc',
    '$CHROME_DIR/browser/printing/printing_layout_uitest.cc',
    '$CHROME_DIR/browser/resource_dispatcher_host_uitest.cc',
    '$CHROME_DIR/browser/sanity_uitest.cc',
    '$CHROME_DIR/browser/session_history_uitest.cc',
    '$CHROME_DIR/browser/sessions/session_restore_uitest.cc',
    '$CHROME_DIR/browser/ssl_uitest.cc',
    '$CHROME_DIR/browser/tab_restore_uitest.cc',
    '$CHROME_DIR/browser/view_source_uitest.cc',
    '$CHROME_DIR/common/logging_chrome_uitest.cc',
    '$CHROME_DIR/common/net/cache_uitest.cc',
    '$CHROME_DIR/common/pref_service_uitest.cc',
    '$CHROME_DIR/test/accessibility/accessibility_tests.cc',
    '$CHROME_DIR/test/accessibility/accessibility_util.cc',
    '$CHROME_DIR/test/accessibility/browser_impl.cc',
    '$CHROME_DIR/test/accessibility/keyboard_util.cc',
    '$CHROME_DIR/test/accessibility/registry_util.cc',
    '$CHROME_DIR/test/accessibility/tab_impl.cc',
    '$CHROME_DIR/test/automation/automation_proxy_uitest.cc',
    '$CHROME_DIR/test/perf/mem_usage.cc',
    '$CHROME_DIR/test/reliability/page_load_test$OBJSUFFIX',
    '$NET_DIR/url_request/url_request_test_job$OBJSUFFIX',
    ])

  # Windows-specific tests.
  ui_test_files.extend([
      '$CHROME_DIR/browser/views/find_bar_win_interactive_uitest.cc',
      '$CHROME_DIR/browser/views/find_bar_win_uitest.cc',
  ])

env.ChromeTestProgram('ui_tests', ui_test_files)

env.ChromeMSVSProject('$CHROME_DIR/test/ui/ui_tests.vcproj',
                      dependencies = [
                          ('$WEBKIT_DIR/glue/plugins/test/' +
                               'npapi_test_plugin.vcproj'),
                          '$CHROME_DIR/test/automation/automation.vcproj',
                          '$BASE_DIR/build/base.vcproj',
                          '$BZIP2_DIR/bzip2.vcproj',
                          '$NET_DIR/build/net.vcproj',
                          '$CHROME_DIR/browser/browser.vcproj',
                          '$MODP_B64_DIR/modp_b64.vcproj',
                          '$CHROME_DIR/app/chrome_exe.vcproj',
                          '$ZLIB_DIR/zlib.vcproj',
                          '$CHROME_DIR/common/common.vcproj',
                          '$ICU38_DIR/build/icu.vcproj',
                          '$CHROME_DIR/app/locales/he.vcproj',
                          '$CHROME_DIR/app/locales/da.vcproj',
                          '$BASE_DIR/build/base_gfx.vcproj',
                          ('$WEBKIT_DIR/tools/npapi_layout_test_plugin/' +
                               'npapi_layout_test_plugin.vcproj'),
                          '$TESTING_DIR/gtest.vcproj',
                          '$LIBPNG_DIR/libpng.vcproj',
                          '$CHROME_DIR/app/locales/en-US.vcproj',
                          '$SKIA_DIR/skia.vcproj',
                          ('$CHROME_DIR/test/security_tests/' +
                               'security_tests.vcproj'),
                          '$CHROME_DIR/app/locales/zh-TW.vcproj',
                          '$GOOGLEURL_DIR/build/googleurl.vcproj',
                          '$SDCH_DIR/sdch.vcproj',
                          '$LIBXML_DIR/build/libxml.vcproj',
                          '$CHROME_DIR/browser/views/browser_views.vcproj',
                      ],
                      guid='{76235B67-1C27-4627-8A33-4B2E1EF93EDE}')
