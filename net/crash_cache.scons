# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration the disk_cache{,.exe} executable.
"""

Import('env')

env = env.Clone()

env.SConscript([
    # On Linux, dependencies must follow dependents, so...
    # 1) net must come before base and modp_b64.
    '$NET_DIR/using_net.scons',

    '$BASE_DIR/using_base.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$ICU38_DIR/using_icu38.scons',
], {'env':env})

env.Prepend(
    CPPPATH = [
        '$CHROME_SRC_DIR',
    ],
)

if env['PLATFORM'] == 'win32':
  env.Prepend(
      CCFLAGS = [
          '/TP',
          '/WX',
      ],
      CPPDEFINES = [
          '_WIN32_WINNT=0x0600',
          'WINVER=0x0600',
          '_HAS_EXCEPTIONS=0',
      ],
      LINKFLAGS = [
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',
          '/MACHINE:X86',
          '/FIXED:No',
          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
  )

input_files = [
    'disk_cache/disk_cache_test_util$OBJSUFFIX',
    'tools/crash_cache/crash_cache.cc',
]


if env['PLATFORM'] in ('posix', 'win32'):

  crash_cache = env.ChromeTestProgram('crash_cache', input_files)

  installed_test = env.Install('$TARGET_ROOT', crash_cache)

  env.Alias('net', installed_test)
