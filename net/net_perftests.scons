# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building the net_perftests{,.exe} executable.
"""

Import('env')

env = env.Clone()

env.SConscript([
    # On Linux, dependencies must follow dependents, so...
    # 1) net must come before base and modp_b64.
    # 2) bzip2 must come before base.
    '$NET_DIR/using_net.scons',

    '$BASE_DIR/using_base.scons',
    '$CHROME_SRC_DIR/build/using_googleurl.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$MODP_B64_DIR/using_modp_b64.scons',
], {'env':env})

if env['PLATFORM'] in ('posix', 'darwin'):
  env.SConscript([
      '$LIBEVENT_DIR/using_libevent.scons',
  ], {'env':env})

env.Prepend(
    CPPPATH = [
        '$CHROME_SRC_DIR',
    ],
)

if env['PLATFORM'] == 'win32':
  env.Prepend(
      CCFLAGS = [
          '/TP',
          '/WX',
      ],
      CPPDEFINES = [
          '_WIN32_WINNT=0x0600',
          'WINVER=0x0600',
          '_HAS_EXCEPTIONS=0',
      ],
      LINKFLAGS = [
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',
          '/MACHINE:X86',
          '/FIXED:No',
          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
  )

input_files = [
    'base/cookie_monster_perftest.cc',
    'disk_cache/disk_cache_perftest.cc',
    'disk_cache/disk_cache_test_util$OBJSUFFIX',

    # TODO(sgk): avoid using .cc from base directly
    '$OBJ_ROOT/base/run_all_perftests$OBJSUFFIX',
    '$OBJ_ROOT/base/perftimer$OBJSUFFIX',
]

if env['PLATFORM'] in ('posix', 'win32'):

  env.ChromeTestProgram('net_perftests', input_files)
