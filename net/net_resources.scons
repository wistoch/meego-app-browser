# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building the net_resources.rc resources.
"""

import os
import sys

Import('env')

env = env.Clone()

input_files = [
    'base/effective_tld_names.dat',
    'tools/tld_cleanup/tld_cleanup$PROGSUFFIX',
]

# This dat file needed by net_resources is generated.
tld_names_clean = env.Command(
    '$TARGET_ROOT/grit_derived_sources/effective_tld_names_clean.dat',
    input_files,
    '${SOURCES[1]} ${SOURCES[0]} $TARGET')

sys.path.append(env.Dir('$CHROME_SRC_DIR/tools/grit').abspath)
env.Tool('scons', toolpath=[env.Dir('$CHROME_SRC_DIR/tools/grit/grit')])
# This dummy target is used to tell the emitter where to put the target files.
generated = env.GRIT('$TARGET_ROOT/grit_derived_sources/dummy_net_res',
                     ['base/net_resources.grd'] + tld_names_clean)


if env.Bit('windows'):
  env.Prepend(
      RCFLAGS = [
          '/l 0x409',
      ],
  )

  net_resources = []
  for g in [g for g in generated if str(g).endswith('.rc')]:
    net_res = env.RES(g)
    net_resources.extend(net_res)
    env.Depends(net_res, tld_names_clean)

  p = env.ChromeMSVSProject('build/net_resources.vcproj',
                            dest='$CHROME_SRC_DIR/net/build/net_resources.vcproj',
                            guid='{8BC9CEB8-8B4A-11D0-8D11-00A0C91BC942}',
                            buildtargets=net_resources,
                            ConfigurationType='10')

  p.AddToolFile('../tools/grit/build/grit_resources.rules')

  p.AddConfig('Debug|Win32',
              InheritedPropertySheets=[
                  '$(SolutionDir)../build/common.vsprops',
                  '$(SolutionDir)../build/debug.vsprops',
              ])

  p.AddConfig('Release|Win32',
              InheritedPropertySheets=[
                  '$(SolutionDir)../build/common.vsprops',
                  '$(SolutionDir)../build/release.vsprops',
              ])
