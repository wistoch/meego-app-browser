# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone(
    PCRE_DIR = '$WEBKIT_DIR/port/JavaScriptCore/pcre',
)

env.Prepend(
    CPPPATH = [
        '$WEBKIT_DIR/port/JavaScriptCore',
        '$WEBKIT_DIR/port/JavaScriptCore/pcre',
        '$WEBKIT_DIR/build/JSConfig/WebCore/v8',
    ])

if env['PLATFORM'] == 'win32':
    env.Prepend(
        CCFLAGS = [
                '/TP',
                '/wd4127',
                '/wd4355',
                '/wd4510',
                '/wd4512',
                '/wd4610',
                '/wd4706',
                '/wd4800',
                ],
        )

dir = env.Dir('$PCRE_DIR')

dir.addRepository(env.Dir('$CHROME_SRC_DIR/webkit/pending'))
dir.addRepository(env.Dir('$CHROME_SRC_DIR/third_party/WebKit/JavaScriptCore/pcre'))

input_files = [
     '$PCRE_DIR/pcre_compile.cpp',
     '$PCRE_DIR/pcre_xclass.cpp',
     '$PCRE_DIR/pcre_ucp_searchfuncs.cpp',
     '$PCRE_DIR/pcre_tables.cpp',
     '$PCRE_DIR/pcre_exec.cpp',
]

env.ChromeStaticLibrary('JavaScriptCore_pcre', input_files)

