# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import(['env', 'env_res'])

env = env.Clone()
env_res = env_res.Clone()

env_res.Append(
    CPPPATH = [
        "$CHROME_SRC_DIR",
    ],
)

if env_res.Bit('windows'):
  env_res.Append(
      RCFLAGS = [
          ["/l", "0x409"],
      ],
  )

import sys
sys.path.append(env.Dir('$CHROME_SRC_DIR/tools/grit').abspath)
env_grd = env.Clone()
env_grd.Tool('scons', toolpath=[env_grd.Dir('$CHROME_SRC_DIR/tools/grit/grit')])
# This dummy target (webkit_resources) is used to tell the emitter where
# to put the target files.
generated = env_grd.GRIT(
    '$TARGET_ROOT/grit_derived_sources/fake_generated_resources',
    '$CHROME_SRC_DIR/webkit/glue/webkit_resources.grd')

if env_res.Bit('windows'):
  for g in [g for g in generated if str(g).endswith('.rc')]:
    env_res.RES(g)

env.ChromeMSVSProject('$WEBKIT_DIR/build/localized_resources/localized_resources.vcproj',
                guid='{0B469837-3D46-484A-AFB3-C5A6C68730B9}')
