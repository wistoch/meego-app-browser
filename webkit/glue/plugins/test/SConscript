# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env', 'env_res')

env = env.Clone()
env_res = env_res.Clone()

input_files = [
    'npapi_constants.cc',
    'npapi_test.cc',
    'plugin_arguments_test.cc',
    'plugin_delete_plugin_in_stream_test.cc',
    'plugin_get_javascript_url_test.cc',
    'plugin_geturl_test.cc',
    'plugin_new_fails_test.cc',
    'plugin_npobject_proxy_test.cc',
    'plugin_test.cc'
]

if env.Bit('windows'):
  # TODO(port): Port these.
  input_files.extend([
      'plugin_execute_script_delete_test.cc',
      'plugin_javascript_open_popup.cc',
      'plugin_client.cc',                 # Includes not ported headers.
      'plugin_npobject_lifetime_test.cc', # Has win32-isms (HWND, CALLBACK).
      'plugin_window_size_test.cc'        # Has w32-isms including HWND.
  ])

env.Append(
    LIBS = [
         'base',
         'icu',
    ],
)

if env.Bit('windows'):
  input_files.extend([
      env_res.RES('npapi_test.rc'),
      'npapi_test.def'
  ])

  env.Append(
      CCFLAGS = [
          '/TP',
          '/wd4800',
      ],

      LIBS = [
          'comctl32.lib',
          'rpcrt4.lib',
          'shlwapi.lib',
          'winmm.lib',
      ],

      LINKFLAGS = [
          '/INCREMENTAL',
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',
          '/FIXED:No',
          '/SUBSYSTEM:CONSOLE',
          '/MACHINE:X86',
          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
  )

dll = env.ChromeSharedLibrary('npapi_test_plugin', input_files)

# Install ourselves into the destination root so that dependent tests
# can find us.
i = env.Install('$DESTINATION_ROOT/plugins', dll)
env.Alias('webkit', i)

env.ChromeMSVSProject('$WEBKIT_DIR/glue/plugins/test/npapi_test_plugin.vcproj',
                dependencies = [
                    '$BASE_DIR/build/base.vcproj',
                    '$ICU38_DIR/build/icu.vcproj',
                ],
                guid='{0D04AEC1-6B68-492C-BCCF-808DFD69ABC6}')
