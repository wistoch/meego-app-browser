# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env', 'env_res')

env = env.Clone()
env_res = env_res.Clone()

input_files = [
    'main.cpp',
    'PluginObject.cpp',
    'TestObject.cpp'
]

if env['PLATFORM'] == 'win32':
  input_files.extend([
      env_res.RES('npapi_layout_test_plugin.rc'),
      'npapi_layout_test_plugin.def'
  ])

  env.Append(
      CCFLAGS = [
          '/TP',
          '/WX',
      ],

      LIBS = [
          'comctl32.lib',
          'rpcrt4.lib',
          'shlwapi.lib',
          'winmm.lib',
      ],

      LINKFLAGS = [
          '/DELAYLOAD:"dwmapi.dll"',
          '/DELAYLOAD:"uxtheme.dll"',
          '/FIXED:No',
          '/SUBSYSTEM:CONSOLE',
          '/MACHINE:X86',
          '/safeseh',
          '/dynamicbase',
          '/ignore:4199',
          '/nxcompat',
      ],
  )

if env['PLATFORM'] == 'win32':
  dll = env.ChromeSharedLibrary([
      'npapi_layout_test_plugin',
      'npapi_layout_test_plugin.lib',
      'npapi_layout_test_plugin.ilk',
      'npapi_layout_test_plugin.pdb'
  ], input_files)

  # TODO(sgk):  goes away when env.ChromeSharedLibrary() knows how
  # to do this for us.
  env.Install('$LIBS_DIR', dll[1])
else:
  dll = env.ChromeSharedLibrary(['npapi_layout_test_plugin'], input_files)

i = env.Install('$TARGET_ROOT/plugins', dll)
env.Alias('webkit', i)

i = env.Install('$TARGET_ROOT', dll)
env.Alias('webkit', i)

